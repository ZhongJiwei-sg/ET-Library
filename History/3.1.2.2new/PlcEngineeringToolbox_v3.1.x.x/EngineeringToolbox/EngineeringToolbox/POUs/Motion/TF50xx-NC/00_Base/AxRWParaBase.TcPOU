<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.9">
  <POU Name="AxRWParaBase" Id="{e1039709-b536-4148-b76e-ab15021bc062}" SpecialFunc="None">
    <Declaration><![CDATA[{attribute 'no_check'}
//FB template to read and write parameter
//The FB can be enherided and derivatived methods of SeqRead/SeqWrite used for application specific parameter.
//The extended methods need to call the super class at the beginning and return true if execution will be finished. The allowed states for new parameter are 1..99
FUNCTION_BLOCK AxRWParaBase EXTENDS FbBase
VAR_OUTPUT CONSTANT
	FNC_READ												:	INT := 0;
	FNC_WRITE												:	INT := 1;
END_VAR                                 					
VAR_INPUT                               					
	Fnc														:	INT (FNC_READ..FNC_WRITE) := FNC_READ;    
	EnableAutoInit											:	BOOL := TRUE; (* FB will start internal init (read all parameter) at first call if set to true   *)                                	                                        	
END_VAR                                 					
VAR_OUTPUT                              										
	InitDone												:	BOOL := FALSE; (* Parameter initalisation done - Set parameter are initaliced by active values *)
	ErrorText												:	STRING := '';
END_VAR                         	


VAR
(* Zwischenspeicher ************************************************************)
	MemFnc													:	INT := -1;
	LastError												:	STRING;
	
(* Deklaration Trigger **********************************************************)

(* Zeiten ******************************************************************)

(* Bausteine ***************************************************************)

(* Dummy Speicher **********************************************************)
	_INT													: INT;
	_UINT													: UINT;
	_UDINT													: UDINT;
	_LREAL													: LREAL;
	_DWORD													: DWORD;
END_VAR

]]></Declaration>
    <Implementation>
      <ST><![CDATA[	(************************************************************************************)
	(*																					*)
	(*		B E C K H O F F   N E W   A U T O M A T I O N   T E C H N O L O G Y    		*)
	(*																					*)
	(*		Eiserstr.5	D-33415 Verl	Germany	phone:	+49-5246-0	www.Beckhoff.Com	*)
    (*																					*)
    (*		Bausteinbeschreibung	:	Read/Write parameter template					*)
	(*																					*)
	(************************************************************************************)

(* Execute ***************************************************************************************)
	rtExecute(CLK := Execute);
	IF(rtExecute.Q OR NOT InitDone AND EnableAutoInit)
		AND MemFnc < 0
	THEN
		MemFnc 												:= SEL(Fnc = FNC_WRITE AND InitDone, FNC_READ , FNC_WRITE) ;
		Busy												:= TRUE;
		Done												:= FALSE;
		Error												:= FALSE;
		ErrorID												:= 0;
		LastError											:= ErrorText;
		ErrorText											:= '';
	END_IF

(* Call Method ***********************************************************************************)
	CASE MemFnc OF
	FNC_READ:
		Read(Execute);

	FNC_WRITE:
		Write(Execute);

	-1:
		IF NOT Execute
		THEN
			Busy											:= FALSE;
			Done											:= FALSE;
			Error											:= FALSE;
			ErrorID											:= 0;
			LastError										:= ErrorText;
			ErrorText										:= '';
		END_IF                              				
	ELSE                                    				
		Busy												:= FALSE;
		Done												:= FALSE;
		Error												:= Execute;
		ErrorID												:= E_AdsErr.DEVICE_INVALIDPARM;
		ErrorText											:= CONCAT('Prg error, invalid MemFnc value ', TO_STRING(MemFnc));
	END_CASE                                    			
	
(*************************************************************************************************)

]]></ST>
    </Implementation>
    <Property Name="deviceAddrIsValid" Id="{23de1223-b86a-4541-879c-89b62717f5e6}">
      <Declaration><![CDATA[PROPERTY PROTECTED deviceAddrIsValid : BOOL]]></Declaration>
      <Get Name="Get" Id="{79d2626a-159c-4cb0-994c-7fa0248ab1bd}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[deviceAddrIsValid := TRUE;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="read" Id="{7176313e-5ec1-48bd-8f8b-d2bcfcb661f5}">
      <Declaration><![CDATA[METHOD PUBLIC FINAL read : BOOL //Return value = TRUE if busy
VAR_INPUT
	Execute													: BOOL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF MemFnc = FNC_WRITE AND busy
THEN
	Busy													:= FALSE;
	Error													:= TRUE;
	ErrorId													:= E_AdsErr.DEVICE_BUSY;
	ErrorText												:= CONCAT('Error read parameter in state:' , TO_STRING(state * -1));
	ErrorText												:= CONCAT(ErrorText , ' write parameter is active, Id:');
	ErrorText												:= CONCAT(ErrorText , TO_STRING(ErrorId));
	MemFnc													:=  -1;
	Read													:= FALSE;
ELSE
	MemFnc													:= FNC_READ;
	THIS^.Execute											:= Execute;

	(* Parameter lesen Statemaschine *************************************************************************)
	CASE state OF
	0:	(* Init *)	
		IF(Execute OR NOT InitDone AND EnableAutoInit AND deviceAddrIsValid)
		THEN
		
			IF deviceAddrIsValid
			THEN
				Busy										:= TRUE;
				Done										:= FALSE;
				Error										:= FALSE;
				ErrorId 									:= 0;
				ErrorText									:= '';
				state										:= 1001;
			ELSE
				Busy										:= FALSE;
				Done										:= FALSE;
				Error										:= TRUE;
				ErrorId 									:= 24; //Invalid ads port 
				ErrorText									:= CONCAT('Error read parameter in state:' , TO_STRING(state * -1));
				ErrorText									:= CONCAT(ErrorText , '| ID/Addr. of device invalid');
				state										:= 9999;			
			END_IF
		ELSE                        						
			Busy											:= FALSE;
			Done											:= FALSE;
			Error											:= FALSE;
			ErrorId 										:= 0;
			ErrorText										:= '';
		END_IF

	1..99,1001..1099: (* Call sequence read *)
		IF SeqRead(state)
		THEN
			state											:= 9999;	
			InitDone										:= TRUE;
			Done											:= NOT Error;
			Busy											:= FALSE;		
		END_IF
             				
	9999: (* Last state execution finished *****************************)
		IF NOT Execute 
		THEN
			Busy											:= FALSE;
			Done											:= FALSE;
			Error											:= FALSE;
			ErrorId 										:= 0;
			ErrorText										:= '';
		END_IF
		
	ELSE (* Case else *)	
		Busy												:= FALSE;
		Error												:= TRUE;
		ErrorId												:= E_AdsErr.DEVICE_INVALIDSTATE;
		ErrorText											:= CONCAT('Error read parameter; invalid state:' , TO_STRING(state));
		ErrorText											:= CONCAT(ErrorText , ' Id:');
		ErrorText											:= CONCAT(ErrorText , TO_STRING(ErrorId));
		State												:= 9999;
	END_CASE

	(* Reset state to zero if Execute is set back to false *)
	IF NOT Execute AND NOT busy
	THEN
		MemFnc												:=  -1;
		state 												:= 0;
	END_IF
	
	Read													:= Busy; //Return;
END_IF	

(****************************************************************************************************************)]]></ST>
      </Implementation>
    </Method>
    <Method Name="seqRead" Id="{7fb23e56-e364-4359-8a05-c5eee9ce5a1c}">
      <Declaration><![CDATA[METHOD PROTECTED seqRead : BOOL //Return value = TRUE if execution finished 
VAR_IN_OUT
	state							:	INT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[CASE state OF
1,1001:
(* Example
		IF getValue(IDxGrp:= 16#5100,	IDxOffs := 16#9,	LEN := SIZEOF(uiValue),	pActValue := ADR(uiValue),  pSetValue := 0)
		THEN
			SetRefFlag :=RefFlag							:= TO_BOOL(uiValue);
		END_IF
*)
	SeqRead													:= TRUE;
ELSE
	SeqRead													:= TRUE;
END_CASE]]></ST>
      </Implementation>
    </Method>
    <Method Name="seqWrite" Id="{f0a8e2bb-2478-41e4-a0ad-59b0d7800318}">
      <Declaration><![CDATA[METHOD PROTECTED seqWrite : BOOL //Return value = TRUE if execution finished 
VAR_IN_OUT
	state							:	INT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[CASE state OF
1,1001:
(* Example 
	setValue(IDxGrp:= 16#4000,	IDxOffs := 16#102,	LEN := SIZEOF(SetDec), pActValue := ADR(Dec),	pSetValue := ADR(SetDec));
*)
	SeqWrite												:= TRUE;
ELSE
	SeqWrite												:= TRUE;
END_CASE]]></ST>
      </Implementation>
    </Method>
    <Method Name="write" Id="{c9813364-c775-44b0-9e78-e2c0b33149de}">
      <Declaration><![CDATA[METHOD PUBLIC FINAL write : BOOL //Return value = TRUE if busy
VAR_INPUT
	Execute													:	BOOL;
END_VAR
               ]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF MemFnc = FNC_READ AND busy
THEN
	Busy													:= FALSE;
	Error													:= TRUE;
	ErrorId													:= E_AdsErr.DEVICE_BUSY;
	ErrorText												:= CONCAT('Error write parameter in state:' , TO_STRING(state * -1));
	ErrorText												:= CONCAT(ErrorText , ' read parameter is active, Id:');
	ErrorText												:= CONCAT(ErrorText , TO_STRING(ErrorId));
	MemFnc													:=  -1;
	Write													:= FALSE;
ELSE
	MemFnc													:= FNC_WRITE;
	THIS^.Execute											:= Execute;

(* Parameter schreiben ************************************************************************************************)
	(* Parameter schreiben Statemaschine *)
	CASE state OF
	0:	(* Init *)	
		IF Execute 
		THEN
			IF deviceAddrIsValid
			THEN
				Busy										:= TRUE;
				Done										:= FALSE;
				Error										:= FALSE;
				ErrorId 									:= 0;
				state										:= 1001;
			ELSE
				Busy										:= FALSE;
				Done										:= FALSE;
				Error										:= TRUE;
				ErrorId 									:= 24; //Invalid ads port 	
				ErrorText									:= CONCAT('Error write parameter in state:' , TO_STRING(state * -1));
				ErrorText									:= CONCAT(ErrorText , '| ID/Addr. of device invalid');
				state										:= 9999;			
			END_IF                  						
		ELSE 
			Busy											:= FALSE;
			Done											:= FALSE;
			Error											:= FALSE;
			ErrorId 										:= 0;
			ErrorText										:= '';
		END_IF
		
	1..99,1001..1099: (* Call sequence write *)
		IF SeqWrite(state)
		THEN
			Done											:= NOT Error;
			Busy											:= FALSE;
			state											:= 9999;			
		END_IF	
		
	9999: (* Last state execution finished *****************************)
		IF NOT Execute 
		THEN
			Busy											:= FALSE;
			Done											:= FALSE;
			Error											:= FALSE;
			ErrorId 										:= 0;
			ErrorText										:= '';
		END_IF
		
	ELSE (* Case else *)	
		Busy												:= FALSE;
		Error												:= TRUE;
		ErrorId												:= E_AdsErr.DEVICE_INVALIDSTATE;
		ErrorText											:= CONCAT('Error write parameter; invalid state:' , TO_STRING(state));
		ErrorText											:= CONCAT(ErrorText , ' Id:');
		ErrorText											:= CONCAT(ErrorText , TO_STRING(ErrorId));
		State												:= 9999;
	END_CASE

	(* Reset state to zero if Execute is set back to false *)
	IF NOT Execute AND NOT busy
	THEN
		MemFnc												:=  -1;
		state 												:= 0;
	END_IF
	
	Write													:= Busy; //Return;
END_IF	
]]></ST>
      </Implementation>
    </Method>
    <ObjectProperties>
      <XmlArchive>
        <Data>
          <o xml:space="preserve" t="UMLStereoTypeContainerObject">
            <v n="IsType" t="UMLType">BaseArea</v>
            <v n="Stereotype">""</v>
            <d n="Stereotypes" t="Hashtable" />
          </o>
        </Data>
        <TypeList>
          <Type n="Hashtable">System.Collections.Hashtable</Type>
          <Type n="String">System.String</Type>
          <Type n="UMLStereoTypeContainerObject">{30250973-b110-4e31-b562-c102e042dca4}</Type>
          <Type n="UMLType">{0197b136-405a-42ee-bb27-fd08b621d0cf}</Type>
        </TypeList>
      </XmlArchive>
    </ObjectProperties>
    <LineIds Name="AxRWParaBase">
      <LineId Id="3" Count="51" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="AxRWParaBase.deviceAddrIsValid.Get">
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="AxRWParaBase.read">
      <LineId Id="3" Count="83" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="AxRWParaBase.seqRead">
      <LineId Id="3" Count="10" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="AxRWParaBase.seqWrite">
      <LineId Id="3" Count="7" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="AxRWParaBase.write">
      <LineId Id="3" Count="80" />
      <LineId Id="2" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>