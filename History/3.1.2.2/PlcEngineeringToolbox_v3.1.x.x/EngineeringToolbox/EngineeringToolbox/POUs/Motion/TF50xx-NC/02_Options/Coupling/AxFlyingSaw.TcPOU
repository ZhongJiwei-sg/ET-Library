<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.5">
  <POU Name="AxFlyingSaw" Id="{7bd04f45-c003-43c6-9d81-d25eee0207ca}" SpecialFunc="None">
    <Declaration><![CDATA[{attribute 'no_check'}
//Nc-axis flying saw control
//based on Tc2_MC2_FlyingSaw library
FUNCTION_BLOCK AxFlyingSaw EXTENDS AxFbBase
VAR_OUTPUT CONSTANT
	FNC_GEAR_OUT											:	INT := 0;
	FNC_GEAR_IN_VELO										:	INT := 1;
	FNC_GEAR_IN_POS											:	INT := 2;
END_VAR                     								
VAR_INPUT                   								
	Fnc														:	INT (FNC_GEAR_OUT..FNC_GEAR_IN_POS) := FNC_GEAR_OUT;
	RatioNumerator											:	LREAL := 1.0; (* Getriebefaktor Numerator/Zähler *)
	RatioDenominator										:	UINT := 1; (* Getriebefaktor Denominator/Nenner *)
	MasterSyncPosition										: 	LREAL := 0.0;
	SlaveSyncPosition										: 	LREAL := 0.0;
	MasterStartDistance										: 	LREAL := 0.0;
	SyncMode												: 	ST_SyncMode; (*... mode ...
																						GearInSyncMode : E_GearInSyncMode;
                            								
																					... 32 bit check mask ... 
																						GearInSync_CheckMask_MinPos : BOOL;
																						GearInSync_CheckMask_MaxPos : BOOL;
																						GearInSync_CheckMask_MaxVelo : BOOL;
																						GearInSync_CheckMask_MaxAcc : BOOL;
																						GearInSync_CheckMask_MaxDec : BOOL;
																						GearInSync_CheckMask_MaxJerk : BOOL;
																						GearInSync_CheckMask_OvershootPos : BOOL;
																						GearInSync_CheckMask_UndershootPos : BOOL;
																						GearInSync_CheckMask_OvershootVelo : BOOL;
																						GearInSync_CheckMask_UndershootVelo : BOOL;
																						GearInSync_CheckMask_OvershootVeloZero : BOOL;
																						GearInSync_CheckMask_UndershootVeloZero : BOOL;
																				
																					... operation masks ...
																						GearInSync_OpMask_RollbackLock : BOOL;
																						GearInSync_OpMask_InstantStopOnRollback : BOOL;
																						GearInSync_OpMask_PreferConstVelo : BOOL;
																						GearInSync_OpMask_IgnoreMasterAcc : BOOL;*)
	Velo													:	LREAL := 0.0;	(* Flying Saw ... Max Slave-Geschwindigkeit in Synchronisierungsphase. Bei 0 --> Systemmanager-Daten. Achtung: Geschwindigkeit wird nur überprüft, wenn die Überprüfung mit der Variablen SyncMode aktiviert wird.*)
	Acc														:	LREAL := 0.0; (* Bei 0 werden wird die Beschleunigung aus dem TCManager uebernommen *)
	Dec														:	LREAL := 0.0; (* Bei 0 werden wird die Verzoegerung aus dem TCManager uebernommen *)
	Jerk													:	LREAL := 0.0; (* Bei 0 werden wird der Ruck aus dem TCManager uebernommen *)
(*	BufferMode												:	MC_BufferMode := MC_Aborting;(* 	MC_Aborting,
																												MC_Buffered,
																												MC_BlendingLow,
																												MC_BlendingPrevious,
																												MC_BlendingNext,
																												MC_BlendingHigh *)
*)                          								
(*	Options 												: 	ST_MOVEOptions; *)
//	rCamTableCharac 										:  REFERENCE TO MC_FlyingSawCharacValues; (* Struktur mit den charakteristischen Kennwerten.*)
	rMasterAxis												:	REFERENCE TO AXIS_REF := 0;
END_VAR                     								
VAR_IN_OUT                  								
END_VAR                     								
VAR_OUTPUT                  								
	StartSync 												:  BOOL;  (* Flying Saw*)
END_VAR
VAR
(* cache **************************************************************************)
	MemFnc													:	INT := -1;
                            								
(* timer *)                 								
                            	
(* trigger *)               	
                            	
(* MC-function-block instances *****************************************************)
	MC_GearOut												:	MC_GearOut;
	MC_GearInVelo 											:  MC_GearInVelo;
	MC_GearInPos											:  MC_GearInPos;	
//	MC_ReadCharacteristics									:  MC_ReadFlyingSawCharacteristics; (* charakteristischen Kennwerte der Synchronisierungsphase ausgelesen *)

(* Dummy storage *******************************************************************)
(*	i,j		: INT := 0 ; *)
	Dummy 	: BOOL; 
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[	(************************************************************************************)
	(*																					*)
	(*		B E C K H O F F   N E W   A U T O M A T I O N   T E C H N O L O G Y    		*)
	(*																					*)
	(*		Eiserstr.5	D-33415 Verl	Germany	phone:	+49-5246-0	www.Beckhoff.Com	*)
    (*																					*)
    (*		Bausteinbeschreibung	:	Flying Saw Achs Baustein						*)
	(*		TwinCAT-Version			:	3.x or higher									*)
	(*		Version					:	V3.0 / 03/13									*)
	(*		Lib						:	TcMC2_FlyingSaw.lib 							*)
 	(*		Autor					:	DS												*)
	(*																					*)
	(************************************************************************************)

(* Execute ***************************************************************************************)
	rtExecute(CLK := Execute);
	IF rtExecute.Q
		AND MemFnc < 0
	THEN
		MemFnc 												:= Fnc ;
		Busy												:= TRUE;
		Done												:= FALSE;
		Error												:= FALSE;
		ErrorID												:= 0;
		CmdAborted											:= FALSE;
		ErrorText											:= '';
	END_IF

(* Call Method ***********************************************************************************)
	CASE MemFnc OF
	FNC_GEAR_OUT:
		GearOut(Execute);

	FNC_GEAR_IN_VELO:
		GearInVelo(Execute);

	FNC_GEAR_IN_POS:
		GearInPos(Execute);

	-1:
		IF NOT Execute
		THEN
			StartSync										:= FALSE;
			Busy											:= FALSE;
			Done											:= FALSE;
			Error											:= FALSE;
			ErrorID											:= 0;
			CmdAborted										:= FALSE;
			ErrorText										:= '';
		END_IF                          					
	ELSE                                					
		StartSync											:= FALSE;
		Busy												:= FALSE;
		Done												:= FALSE;
		Error												:= Execute;
		ErrorID												:= E_AdsErr.DEVICE_INVALIDPARM;
		CmdAborted											:= FALSE;
		ErrorText											:= CONCAT('invalid parameter value(s) Fnc no: ' , INT_TO_STRING(MemFnc));
	END_CASE

(*************************************************************************************************)

]]></ST>
    </Implementation>
    <Method Name="gearInPos" Id="{dce7ccca-5220-4eb9-951b-fe4c5f6558a9}">
      <Declaration><![CDATA[METHOD PUBLIC gearInPos : BOOL //Return value = TRUE if busy
//required parameter
//rAxis
//rMasterAxis
//RatioNumerator
//RatioDenominator
//MasterSyncPosition
//SlaveSyncPosition
//SyncMode
//MasterStartDistance
//Velo
//Acc
//Dec
//Jerk
VAR_INPUT
	Execute													:	BOOL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[MemFnc														:= FNC_GEAR_IN_POS;
THIS^.Execute												:= Execute;
	
IF NOT __ISVALIDREF(rAxis) 
THEN
	Busy													:= NOT Error;
	Error													:= TRUE;
	ErrorId													:= E_AdsErr.DEVICE_INVALIDPARM;
	CmdAborted												:= FALSE;
	Done													:= FALSE;
	Active													:= FALSE;
	ErrorText												:= 'Reference rAxis invalid (0)';
	MemFnc													:= SEL(Execute , -1 ,MemFnc); 
	RETURN;
ELSIF NOT __ISVALIDREF(rMasterAxis)
THEN
	Busy													:= NOT Error;
	Error													:= TRUE;
	ErrorId													:= E_AdsErr.DEVICE_INVALIDPARM;
	CmdAborted												:= FALSE;
	Done													:= FALSE;
	ErrorText												:= 'Reference rMasterAxis invalid (0)';
	MemFnc													:= SEL(Execute , -1 ,MemFnc); 
	RETURN;
END_IF

	MC_GearInPos(
		Execute				:= Execute,
		RatioNumerator		:= RatioNumerator,
		RatioDenominator	:= RatioDenominator,
		MasterSyncPosition	:= MasterSyncPosition,
		SlaveSyncPosition	:= SlaveSyncPosition,
		SyncMode			:= SyncMode,
		MasterStartDistance	:= MasterStartDistance,
		Velocity			:= Velo,
		Acceleration		:= Acc,
		Deceleration		:= Dec,
		Jerk				:= Jerk,
		BufferMode			:= ,
		Options				:= ,
		Master				:= rMasterAxis,
		Slave				:= rAxis,
		StartSync			=> StartSync,
		InSync				=> Done,
		Busy				=> Busy,
		Active				=> Active,
		CommandAborted		=> CmdAborted,
		Error				=> Error,
		ErrorID				=> ErrorID);
		
	

	IF Error THEN
		LastErrorId											:= ErrorId;
		ErrorText 											:= CONCAT('MC_GearInPos ID:' , UDINT_TO_STRING(ErrorId));
	ELSE
		ErrorText 											:= '';
	END_IF

	IF NOT Execute AND NOT Busy
	THEN
		MemFnc												:= -1;
	END_IF
	
	GearInPos												:= Busy;	]]></ST>
      </Implementation>
    </Method>
    <Method Name="gearInVelo" Id="{af411a5f-72d3-4a11-a7ef-595540c1e813}">
      <Declaration><![CDATA[METHOD PUBLIC gearInVelo : BOOL //Return value = TRUE if busy
//required parameter
//rAxis
//rMasterAxis
//RatioNumerator
//RatioDenominator
//SyncMode
//Velo
//Acc
//Dec
//Jerk
VAR_INPUT
	Execute													:	BOOL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[MemFnc														:= FNC_GEAR_IN_VELO;
THIS^.Execute												:= Execute;
	
IF NOT __ISVALIDREF(rAxis)
THEN
	Busy													:= NOT Error;
	Error													:= TRUE;
	ErrorId													:= E_AdsErr.DEVICE_INVALIDPARM;
	CmdAborted												:= FALSE;
	Done													:= FALSE;
	Active													:= FALSE;
	ErrorText												:= 'Reference rAxis invalid (0)';
	MemFnc													:= SEL(Execute , -1 ,MemFnc); 
	RETURN;
ELSIF NOT __ISVALIDREF(rMasterAxis)
THEN
	Busy													:= NOT Error;
	Error													:= TRUE;
	ErrorId													:= E_AdsErr.DEVICE_INVALIDPARM;
	CmdAborted												:= FALSE;
	Done													:= FALSE;
	ErrorText												:= 'Reference rMasterAxis invalid (0)';
	MemFnc													:= SEL(Execute , -1 ,MemFnc); 
	RETURN;
END_IF

	MC_GearInVelo(
		Execute			:= Execute,
		RatioNumerator	:= RatioNumerator ,
		RatioDenominator:= RatioDenominator ,
		SyncMode		:= SyncMode,
		Velocity		:= Velo,
		Acceleration	:= Acc,
		Deceleration	:= Dec,
		Jerk			:= Jerk,
		BufferMode		:= ,
		Options			:= ,
		Master			:= rMasterAxis,
		Slave			:= rAxis,
		StartSync		=> StartSync,
		InSync			=> Done,
		Busy			=> Busy,
		Active			=> Active,
		CommandAborted	=> CmdAborted,
		Error			=> Error,
		ErrorID			=> ErrorId);
	
	IF Error THEN
		LastErrorId											:= ErrorId;
		ErrorText 											:= CONCAT('MC_GearInVelo ID:' , UDINT_TO_STRING(ErrorId));
	ELSE
		ErrorText 											:= '';
	END_IF

	IF NOT Execute AND NOT Busy
	THEN
		MemFnc												:= -1;
	END_IF
	
	GearInVelo												:= Busy;	
]]></ST>
      </Implementation>
    </Method>
    <Method Name="gearOut" Id="{a7ef3d55-05fb-4479-ad2f-021090ee0f7e}">
      <Declaration><![CDATA[METHOD PUBLIC gearOut : BOOL //Return value = TRUE if busy
//required parameter
//rAxis
VAR_INPUT
	Execute													:	BOOL;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[MemFnc														:= FNC_GEAR_OUT;
THIS^.Execute												:= Execute;

IF NOT __ISVALIDREF(rAxis) 
THEN
	Busy													:= NOT Error;
	Error													:= TRUE;
	ErrorId													:= E_AdsErr.DEVICE_INVALIDPARM;
	CmdAborted												:= FALSE;
	Done													:= FALSE;
	Active													:= FALSE;
	ErrorText												:= 'Reference rAxis invalid (0)';
	MemFnc													:= SEL(Execute , -1 ,MemFnc); 
	RETURN;
END_IF

	MC_GearOut(
		Execute	:= Execute,
		Options	:= ,
		Slave	:= rAxis,
		Done	=> Done,
		Busy	=> Busy,
		Error	=> Error,
		ErrorID	=> ErrorID);

	Active													:= Busy;
	CmdAborted												:= FALSE;
	StartSync												:= FALSE;
	
	IF Error THEN
		LastErrorId											:= ErrorId;
		ErrorText 											:= CONCAT('MC_GearOut ID:' , UDINT_TO_STRING(ErrorId));
	ELSE
		ErrorText 											:= '';
	END_IF

	IF NOT Execute AND NOT Busy
	THEN
		MemFnc												:= -1;
	END_IF                  								
                            	
	GearOut													:= Busy;
]]></ST>
      </Implementation>
    </Method>
    <ObjectProperties>
      <XmlArchive>
        <Data>
          <o xml:space="preserve" t="UMLStereoTypeContainerObject">
            <v n="IsType" t="UMLType">BaseArea</v>
            <v n="Stereotype">""</v>
            <d n="Stereotypes" t="Hashtable" />
          </o>
        </Data>
        <TypeList>
          <Type n="Hashtable">System.Collections.Hashtable</Type>
          <Type n="String">System.String</Type>
          <Type n="UMLStereoTypeContainerObject">{30250973-b110-4e31-b562-c102e042dca4}</Type>
          <Type n="UMLType">{0197b136-405a-42ee-bb27-fd08b621d0cf}</Type>
        </TypeList>
      </XmlArchive>
    </ObjectProperties>
    <LineIds Name="AxFlyingSaw">
      <LineId Id="3" Count="61" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="AxFlyingSaw.gearInPos">
      <LineId Id="3" Count="63" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="AxFlyingSaw.gearInVelo">
      <LineId Id="3" Count="59" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="AxFlyingSaw.gearOut">
      <LineId Id="3" Count="41" />
      <LineId Id="2" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>